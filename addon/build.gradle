/*
 * This file is part of GrieferUtils (https://github.com/L3g7/GrieferUtils).
 *
 * Copyright 2020-2023 L3g7
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *   http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

// Dependencies
buildscript {
	dependencies {
		classpath 'com.github.gatooooooo:ForgeGradle:FG_2.1-SNAPSHOT' // Makes ForgeGradle work with Gradle 6
		classpath 'com.github.xcfrg:mixingradle:ae2a80e' // Makes MixinGradle work with ForgeGradle 2.1 (Minecraft 1.8)
	}
}

plugins {
	id "java"
	id "de.undercouch.download" version "5.3.0"
}
apply plugin: 'net.minecraftforge.gradle.forge'
apply plugin: 'org.spongepowered.mixin'

repositories {
	mavenCentral()

	gradlePluginPortal()
	maven { url = "https://repo.spongepowered.org/repository/maven-public/" }
	maven { url = "https://jitpack.io/" }
	maven { url = "https://maven.minecraftforge.net/" }
}

// Mixin
dependencies {
	implementation "org.spongepowered:mixin:0.7.11-SNAPSHOT"
	annotationProcessor "org.spongepowered:mixin:0.7.11-SNAPSHOT"
}

mixin {
	disableRefMapWarning = true
	defaultObfuscationEnv searge
	add sourceSets.main, refmap
}

// Minecraft
minecraft {
	version = "1.8.9-11.15.1.2318-1.8.9"
	runDir = "run"
	mappings = "stable_22"
	makeObfSourceJar = false
	clientJvmArgs += ["-Dfml.coreMods.load=net.labymod.core.asm.LabyModCoreMod", "-DdebugMode=true", "-Daddonresources=addon.json"]
}

// mXparser
dependencies.implementation 'org.mariuszgromada.math:MathParser.org-mXparser:5.1.0'
dependencies.implementation 'org.whispersystems:curve25519-android:0.5.0'

// Matrix dependencies
dependencies.implementation 'at.favre.lib:hkdf:1.1.0'
dependencies.implementation 'net.i2p.crypto:eddsa:0.3.0'

// Compile options
java.sourceCompatibility = java.targetCompatibility = JavaVersion.VERSION_1_8
compileJava.options.encoding = "UTF-8"
jar.archiveFileName = "griefer-utils-v${version}.jar"
reobf.jar.mappingType = "NOTCH"

// addon.json's version
processResources.from(sourceSets.main.resources.srcDirs) {
	include 'addon.json'
	expand 'version': version
}

// api jar creation
tasks.build.dependsOn('apiJar')
tasks.register('apiJar', Jar) {
	from sourceSets.main.output
	archiveFileName = "griefer-utils-v${project.version}-api.jar"
}

// download libraries
tasks.build.dependsOn('downloadLibs')
tasks.register('downloadLibs') {
	mkdir "libs"

	download.run {
		src ([
			"https://dl.labymod.net/latest/api/files/lm_api_mc1.8.9.jar", // LabyMod
			"https://mediafilez.forgecdn.net/files/2279/149/Schematica-1.8.9-1.7.7.143-api.jar" // Schematica
		])
		dest 'libs/'
		onlyIfModified true
		quiet true
	}

	download.run {
		src "https://dl.labymod.net/latest/?file=8f645692-0be5-4d49-8f51-999a11943017&a=1" // EmoteChat
		dest new File(projectDir, "libs/EmoteChat.jar")
		onlyIfModified true
		quiet true
	}
}